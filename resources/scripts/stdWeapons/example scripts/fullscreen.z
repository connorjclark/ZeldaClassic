//Test Fullscreen attack
//D0-damage dealt
//D1-sound to play
//D2- enemy defense used against this weapon
ffc script FullScreenTest{
	void run (int damage, int sound, int defense){
		lweapon l = LweaponInit (this, LW_GHOSTED, damage, 89, sound);
		FullScreenAttack(this, l, 1, 60,defense,0);
		TerminateLweapon(this, l);
	}
}

//D0-damage dealt
//D1-sprite for orbiting particle
//D2-sound to play on finishing cast
//D3-secret trigger when cast on combo with given flag
//D4-secret trigger when cast anywhere on screen with this flag
//D5-number of orbiting particles
//D6-lightning tile
ffc script EtherSpell{
	void run (int damage, int sprite, int sound, int triggerSpot, int triggerGlobal, int numparticles, int Lightning){
		Link->Action=LA_CASTING;
		lweapon l = LweaponInit (this, LW_GHOSTED, damage, 0, 53);
		LweaponSetFlag(l, LWF_NO_FALL);
		l->CollDetection=false;
		this->X=Link->X;
		this->Y=Link->Y;
			bool particlesgone=false;
			lweapon particle[32];
			int angle=0;
			int timer = 120;
			for (int i=0; i<numparticles; i++){
				particle[i] = CreateParticle (this->X, this->Y, sprite, 0, 0, 0, 0, -1, 0);
				SetParticleAngularMovement(particle[i], 360/numparticles*i, 1);
			}
			while (!particlesgone){
				Link->Action=LA_FROZEN;
				timer--;
				if (timer<0){
					for (int i=0; i<numparticles; i++){
						if (particle[i]->isValid())SetParticleAngularMovement(particle[i], angle + 360/numparticles*i, 6);
					}
				}
				else {
					angle+=4;
					for (int i=0; i<numparticles; i++){
						if (particle[i]->isValid())SetParticleAngularMovement(particle[i], angle + 360/numparticles*i, 0.5);
					}
				}
				for (int i=0; i<numparticles; i++){
					particlesgone=true;
					if (particle[i]->isValid()){
						particlesgone=false;
						break;
					}
				}
				int drawlightning=Link->Y;
				while (drawlightning>=-16){
					drawlightning-=16;
					Screen->FastTile(1, Link->X, drawlightning, Lightning, 7, OP_OPAQUE);
				}
				LweaponWaitframe (this, l, true);
			}
		l->CollDetection=true;
		Game->PlaySound(sound);
		FullScreenAttack(this, l, 1, 60,NPCD_SWORD, triggerGlobal);
		 TriggerUpdate (l, triggerSpot,SFX_SECRET, true, -1, -1);
		Link->Action=LA_NONE;
		TerminateLweapon(this, l);		
	}
}

//D0-damage dealt
//D1-flame sprite
//D2-sound to play on finishing cast
//D3-secret trigger when cast on combo with given flag
//D4-secret trigger when cast anywhere on screen with this flag
//D5-number of explosions and flames
//D6-flame radius
ffc script BombosSpell{
	void run (int damage, int sprite, int sound, int triggerSpot, int triggerGlobal, int numbooms, int flamedistance){
		Link->Action=LA_CASTING;
		lweapon l = LweaponInit (this, LW_GHOSTED, damage, 0, 53);
		LweaponSetFlag(l, LWF_NO_FALL);
		LweaponSetFlag(l,LWF_INVISIBLE);
		LweaponUnSetFlag(l, LWF_DRAW_SHADOW);
		l->CollDetection=false;
		this->X=Link->X;
		this->Y=Link->Y;
			int angle=0;
			int timer = 15;
			int counter = numbooms;
			while (counter>0){
				Link->Action=LA_CASTING;
				timer--;
				if (timer<=0){
					lweapon fire= Screen->CreateLWeapon(LW_FIRE);
					fire->CollDetection=false;
					fire->X=Link->X+(flamedistance*Cos(angle));
					fire->Y=Link->Y+(flamedistance*Sin(angle));
					fire->Step=0;
					angle += 30;
					counter--;
					timer=6;
				}
				LweaponWaitframe (this, l, true);
			}
			timer=60;
			while (timer>0){
				Link->Action=LA_CASTING;
				timer--;
				LweaponWaitframe (this, l, true);
			}
			counter = numbooms;
			while (counter>0){
				Link->Action=LA_CASTING;
				timer--;
				if (timer<=0){
					lweapon fire= Screen->CreateLWeapon(LW_SBOMBBLAST);
					fire->CollDetection=false;
					fire->X=Rand(256);
					fire->Y=Rand(176);
					counter--;
					timer=8;
				}
				LweaponWaitframe (this, l, true);
			}
		l->CollDetection=true;
		Game->PlaySound(sound);
		FullScreenAttack(this, l, 1, 60,NPCD_FIRE, triggerGlobal);
		 TriggerUpdate (l, triggerSpot,SFX_SECRET, true, -1, -1);
		Link->Action=LA_NONE;
		TerminateLweapon(this, l);		
	}
}

//D0-damage dealt
//D1-sound to play on starting quake
//D2-sound to play on finishing cast
//D3-secret trigger when cast on combo with given flag
//D4-secret trigger when cast anywhere on screen with this flag
//D5-quake duration, in frames
//D6-lightning tile
ffc script QuakeSpell{
	void run (int damage, int QuakeSound, int sound, int triggerSpot, int triggerGlobal, int quaketime, int Lightning ){
		Link->Action=LA_CASTING;
		lweapon l = LweaponInit (this, LW_GHOSTED, damage, 0, 53);
		LweaponSetFlag(l,LWF_INVISIBLE);
		LweaponUnSetFlag(l, LWF_DRAW_SHADOW);
		LweaponSetFlag(l, LWF_NO_FALL);
		LweaponSetFlag(l, LWF_MOVE_OFFSCREEN);
		l->CollDetection=false;
		this->X=Link->X;
		this->Y=Link->Y;
			Link->Jump=4;
			Link->Z=1;
			while(Link->Z>0){
				this->X=Link->X;
				this->Y=Link->Y;
				this->Misc[FFC_MISC_Z]=Max(Link->Z-16,0);
				Link->Action=LA_FROZEN;
				LweaponWaitframe (this, l, true);
			}
			Game->PlaySound(QuakeSound);
			Screen->Quake=quaketime;
			while (Screen->Quake>0){
				Link->Action=LA_FROZEN;
				Screen->FastTile(1, Rand(256), Rand(176), Lightning, 10, OP_OPAQUE);
				LweaponWaitframe (this, l, true);
			}
		l->CollDetection=true;
		Game->PlaySound(sound);
		FullScreenAttack(this, l, 1, 60,NPCD_FIRE, triggerGlobal);
		 TriggerUpdate (l, triggerSpot,SFX_SECRET, true);
		Link->Action=LA_NONE;
		TerminateLweapon(this, l);		
	}
}

