//Simple fire rod
//D0-Damage dealt by both direct hit by wand and flame magic.
//D1-Sprite used for wand.
//D2-Sprite for flame
//D3-projectile speed
//D4-Cost, in MP.

ffc script FireRod{
	void run(int damage, int wandsprite, int flamesprite, int speed, int cost){
		lweapon wand = LweaponInit (this, LW_GHOSTED, damage, wandsprite, 0);
		MeleeWeaponStab(this, wand, wand->Misc[LWEAPON_MISC_ORIGTILE], 8, 12, 88, 0, NPCD_WAND, 0, 2);
		if (!AmmoManager(CR_RUPEES, cost, 104, false)){
			MeleeWeaponEndStrike(this, wand, wand->Misc[LWEAPON_MISC_ORIGTILE], 12, 2, 2);
		}
		int it = GetItemID(wand);
		int args[8] = {damage, flamesprite, speed, 0, 0, 0 ,0, it};
		int buffer[] = "FireRodFlame";
		int scr = Game->GetFFCScript(buffer);
		ffc magic = LaunchScriptedLweapon(scr, args);
		PutFFCInFrontOfLink(magic, 16);
		Game->PlaySound(SFX_FIRE);
		MeleeWeaponEndStrike(this, wand, wand->Misc[LWEAPON_MISC_ORIGTILE], 12, 2, 2);
	}	
}

ffc script FireRodFlame{
	void run(int dam, int sprite, int speed){
		lweapon fire =  CreateNewWeapon (this, LW_GHOSTED, dam, sprite);
		LweaponSetFlag(fire, LWF_NO_FALL);
		LweaponSetFlag(fire, LWF_PENETRATING);
		SetStraightMotion4(this, speed);
		while (true){
			UseEnemyDefense(fire, 0, NPCD_FIRE);
			TriggerUpdate (fire, 4, SFX_SECRET, true);
			if (dam>2) TriggerUpdate (fire, 74, SFX_SECRET, true);
			if (dam>4) TriggerUpdate (fire, 73, SFX_SECRET, true);
			TriggerUpdate (fire, 79, SFX_SECRET, true);
			LweaponWaitframe (this,fire, true);
		}
	}
}
