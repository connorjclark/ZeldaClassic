//Induces stautus effect to enemy.
//FFC f - ffc that runs the weapon script.
//scriptnum - target FFC script slot.
//Args - arguments array pointer. Max 7 vars.
//NPC n - pointer to affected enemy.
void InduceEnemyStatusEffect(ffc f, int scriptNum, int args, npc n){
	if (n->HP<=0) return;
	    // Invalid script
    if(scriptNum<0 || scriptNum>511) return;    
    ffc theFFC;    
    // Find an FFC not already in use
    for(int i=FFCS_MIN_FFC; i<=FFCS_MAX_FFC; i++) {
        theFFC=Screen->LoadFFC(i);
        if ((theFFC->Script==scriptNum)&&(theFFC->InitD[7] == NpcNum(n)))return;//Enemy is already affected by this status effect.
        if(theFFC->Script!=0 ||
         (theFFC->Data!=0 && theFFC->Data!=FFCS_INVISIBLE_COMBO) ||
         theFFC->Flags[FFCF_CHANGER]) continue;
         if (FFCNum(f)>=FFCNum(theFFC)) continue;      
        // Found an unused one; set it up
        theFFC->Data=FFCS_INVISIBLE_COMBO;
        theFFC->Script=scriptNum;        
        if(args!=NULL)
        {
            for(int j=Min(SizeOfArray(args), 7)-1; j>=0; j--)
                theFFC->InitD[j]=args[j];
        }    
        theFFC->InitD[7] = NpcNum(n);    
        return;
    }
    // No FFCs available
    return;	
}

//Shortcut for inducing given status effect on any enemy hit by lweapon (defenses and shields are accounted).
void InduceStatusEffectOnHitEnemy(ffc f, lweapon l, int scriptNum, int args){
	npc en = OnHitEnemy(l);
	if (en->isValid())InduceEnemyStatusEffect(f, scriptNum, args, en);
}

//Returns number of target NPC in the currant enemy.
int NpcNum(npc n){
	for (int i=1; i<= Screen->NumNPCs(); i++){
		npc a = Screen->LoadNPC(i);
		if (n==a) return i;
		else continue;
	}
}