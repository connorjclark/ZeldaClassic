//Turns Lweapon into another one.
lweapon ChangeLweapon(ffc f, lweapon l, int type, int damage, int sprite){
	Remove(l);
	return CreateNewWeapon(f, type, damage, sprite);
}

//Causes Lweapon to explode multiple times like destroyed ghosted boss.
//Super - Set to TRUE for super-bomb-like blast radius.
//Numbooms - number of explosions to create.
//Spread - Maximum explosion distance away from source lweapon.
//Delay - delay between explosions, in frames. 
void KillExplosionMultiple (ffc f, lweapon l, int damage, bool super, int numbooms, int spread, int delay){
	if (l->isValid()) Remove(l);
	int radius = Cond(super, LW_SBOMBBLAST, LW_BOMBBLAST);
	for ( int i = 1; i<=numbooms; i++){
		lweapon boom = Screen->CreateLWeapon(radius);
		boom->Damage=damage;
		if (numbooms==1){
			boom->X = f->X;
			boom->Y = f->Y;
		}
		else {
			boom->X=f->X-(spread)+(Rand(spread*2));
			boom->Y=f->Y-(spread)+(Rand(spread*2));
		}
		Waitframes(delay);
	}
}

//A simpler version of the above function that creates one single explosion.
//Super - Set to TRUE for super-bomb-like blast radius.
void KillExplosion (ffc f, lweapon l, int damage, bool super){
	if (l->isValid()) Remove(l);
	int radius = Cond(super, LW_SBOMBBLAST, LW_BOMBBLAST);
	lweapon boom = Screen->CreateLWeapon(radius);
	boom->Damage=damage;
	boom->X = f->X;
	boom->Y = f->Y;
}

//Cause Lweapon to burst into flames, ala Molotov Cocktail.
void KillMolotov( ffc f, lweapon l, int damage, int numflames, int spread){
	int direction = PI*3/2;
	int anglediff = (PI*2)/numflames;
	Remove(l);
	for (int i = 1; i<=numflames; i++){
		lweapon flame = Screen->CreateLWeapon(LW_FIRE);
		flame->X= f->X;
		flame->Y= f->Y;
		flame->Damage=damage;
		flame->Angular=true;
		flame->Angle=direction;
		flame->Step=spread;
		direction=direction+anglediff;
	}
}

//Spawns an item at Lweapon`s place.
void KillSpawnItem(ffc f, lweapon l, int itemID){
	Remove(l);
	item spawn = Screen->CreateItem(itemID);
	spawn->X= f->X;
	spawn->Y= f->Y;
}

//Starts running another FFC script. Removes Lweapon if successful. USES SEPARATE FFC
void KillRunScript(ffc f, lweapon l, int d0, int d1, int d2, int d3, int d4,int d5, int d6, int d7, int scriptNum){
	int args[8] = {d0,d1,d2,d3,d4,d5,d6,d7};
	int ffcid = RunFFCScript(scriptNum, args);
	if (ffcid>0) TerminateLweapon (f, l);
}

//Removes the scripted lweapon and terminates his master script. Use this instead of usual
//direct call of Quit().
void TerminateLweapon (ffc f, lweapon l){
	Game->PlaySound(SFX_DEBUG_TERMINATE);
	if (l->isValid())Remove(l);
	ClearFFC(f);
	Quit();
}