const int NPCD_ICE = 21; //All defense values between 21 and 40 refer to custom enemy defense array.

const int CF_ICE_ROD_DIRECT_TRIGGER = 88;//Ice rod direct touch trigger flag.
const int CF_ICE_ROD_MAGIC_TRIGGER = 76;//Icy projectile trigger flag.

const int SPR_ICE_PARTICLE = 29;//Sprite for ice sparkle trail

const int TILE_ICE_CUBE = 23540;//Tile to draw on top of frozen enemy.
const int CSET_ICE_CUBE = 7;//Cset used to repaint frozen enemies.

//This Ice rod is mainly created to test custom enemy defenses and status effects to enemies.

//1. Set up 2 sprites. One for wand animation and one for projectile.
//2. Import and compile the script. It requires stdWeapons.zh and all his dependencies. Assign 3 FFC script slots.
//3. Set up generic item settings on Item Editor.
//4. D0 - Damage dealt to enemies on direct hit by projectile or wand strike. 
//        Uses MAGIC or WAND MELEE enemy defense.
//   D1 - Sprite for wand animation. Can be the same for both wands.
//   D2 - Sprite for magic projectile.
//   D3 - Freeze status effect duration.
//   D4 - Projectile flight speed.
//   D5 - Cost per charge, in 1/32th of MP beaker.
//   D7 - Script slot for "IceRod" script.
//5. Assign "Weapon Launcher" action item script.

ffc script IceRod{
	void run (int damage, int wandsprite, int sprite, int freezeduration, int speed, int cost){
		itemdata it = Game->LoadItemData(this->InitD[7]);
		int sound = it->UseSound;
		lweapon wand = LweaponInit (this, LW_GHOSTED, damage, wandsprite, 0);
		MeleeWeaponStab(this, wand, wand->Misc[LWEAPON_MISC_ORIGTILE], 8, 12, CF_ICE_ROD_DIRECT_TRIGGER, 0, NPCD_WAND, 0, 2);
		if (!AmmoManager(CR_MAGIC, cost, 118, false)){
			MeleeWeaponEndStrike(this, wand, wand->Misc[LWEAPON_MISC_ORIGTILE], 12, 2, 2);
		}
		SetLweaponHitboxOffsets(wand,0,0,6,6);
		int itm = GetItemID(wand);
		int args[8] = {damage, sprite, freezeduration, speed, 0, 0, 0, itm};
		int buffer[] = "IceRodMagic";
		int scr = Game->GetFFCScript(buffer);
		ffc magic = LaunchScriptedLweapon(scr, args);
		PutFFCInFrontOfLink(magic, 16);
		Game->PlaySound(sound);
		MeleeWeaponEndStrike(this, wand, wand->Misc[LWEAPON_MISC_ORIGTILE], 12, 2, 2);
	}
}

ffc script IceRodMagic{
	void run (int damage, int sprite, int freezeduration, int speed){
		SetCustomEnemyDefense(NPC_LYNEL2, (NPCD_ICE-21), NPCDT_IGNORE); //L2 lynels are rendered immune to Ice Rod magic.
		SetCustomEnemyDefense(161, (NPCD_ICE-21), NPCDT_ONEHITKILL); //All Fire Zols and Gels will be one-hit-killed by Ice magic.
		SetCustomEnemyDefense(162, (NPCD_ICE-21), NPCDT_ONEHITKILL);
		SetCustomEnemyDefense(163, (NPCD_ICE-21), NPCDT_ONEHITKILL);
		SetCustomEnemyDefense(164, (NPCD_ICE-21), NPCDT_ONEHITKILL);
		SetCustomEnemyDefense(99, (NPCD_ICE-21), NPCDT_ONEHITKILL);//Fire Wizzrobes will suffer the same fate.
		lweapon l = LweaponInit (this, LW_GHOSTED, damage, sprite, 0);
		LweaponSetFlag(l, LWF_AUTO_DIR_UPDATE);
		LweaponSetFlag(l, LWF_AUTO_SPRITE_UPDATE);
		LweaponSetFlag(l, LWF_NO_FALL);
		int oldcmb=-1;
		SetEnemyDefense(l, NPCD_ICE);
		SetLweaponHitboxOffsets(l,0,0,4,4);
		int hitbox[4];
		GetLweaponHitboxOffsets(l, hitbox);
		SetStraightMotion4(this, speed);
		
		npc frozen;
		
		while(true){
			if ((this->Misc[FFC_MISC_TIMER]%15)==0) lweapon particle = CreateParticle (this->X, this->Y,SPR_ICE_PARTICLE, 0, 0, 0, 0, -2, false);
			int args[1] = {freezeduration};
			frozen = OnHitEnemy(l);
			oldcmb=ReflectOffMirrors(oldcmb, this, l);
			
			if (frozen->isValid()){
				int buffer[] = "StatusEffectFrozen";
				int scr = Game->GetFFCScript(buffer);
				InduceEnemyStatusEffect(this, scr, args, frozen);
			}	
			if (LweaponFlagIsSet(l, LWF_REFLECTED))TriggerUpdate (l, CF_REFMAGIC, SFX_SECRET, true);		
			TriggerUpdate (l, CF_ICE_ROD_MAGIC_TRIGGER, SFX_SECRET, true);
			AutoRotateLweaponHitbox (l, hitbox);
			//DrawLweaponHitbox(l);
			LweaponWaitframe(this, l, true);
		}
	}
}

ffc script StatusEffectFrozen{
	void run(int freezeduration){
		npc n = Screen->LoadNPC(this->InitD[7]);
		if (!n->isValid())Quit();
		
		int timer = freezeduration;
		int OrigCset = n->CSet;
		int OrigAspeed = n->ASpeed;
		n->ASpeed=12000;
		n->CSet=CSET_ICE_CUBE;
		this->X = n->X;
		this->Y = n->Y;
		while (n->isValid()){
			if ((timer>=90)||(IsOdd(timer)))Screen->FastTile(4, n->X, n->Y, TILE_ICE_CUBE, CSET_ICE_CUBE, OP_OPAQUE);
			timer--;
			n->Stun=1;
			if (timer<=0){
				n->ASpeed = OrigAspeed;
				n->CSet = OrigCset;
				Quit();
			}
			Waitframe();
		}
	}
}